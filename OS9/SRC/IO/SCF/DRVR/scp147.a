 nam scp147
 ttl parallel interface driver for VME147 printer port

* Copyright 1988, 1989, 1990, 1991, 1992, 1993
* by Microware Systems Corporation
* Reproduced Under License

* This source code is the proprietary confidential property of
* Microware Systems Corporation, and is provided to licensee
* solely for documentation and educational purposes. Reproduction,
* publication, or distribution in any form to any party other than
* the licensee is strictly prohibited.


********************
* SCP147 - Parallel Interface Driver for MVME147
*


********************
* Edition History:
*  #   Date    Comments                                          by
* -- -------- -------------------------------------------------  ---
* 00 88/07/15 genesis.                                           wwb
* 01 88/07/18 debugged and working.                              wwb
* 02 88/07/19 added "delay loop" in irq srvc                     wwb
*              ---- OS-9/68K V2.2 Release ----
*              ---- OS-9/68K V2.3 Release ---- 
* 03 90/03/05  Changed to check for deadly signal ala 2.4 defs   Rwb
*              ---- OS-9/68K V2.4 Release ---- 
*103 91/08/30  Added KANJI condition							 zoo
*              ---- OS-9/68K V2.4J Release ---- 
* 04 92/02/24 Merged ed103 into mainstream.                      wwb
* 05 93/02/10 reworked V_IRQMsk code for MSP support.            wwb
* 06 93/07/01 reworked use pathlists for MWOS structure.         wwb
*              ---- OS-9/68k V3.0 released ----
*              ---- OS-9/68k V3.1 released ----
*
Edition equ 6 current edition number


********************
* SCP147 Device Driver
*
Typ_Lang set (Drivr<<8)+Objct device driver, object code
Attr_Rev set ((ReEnt+SupStat)<<8)+0 attributes and revision

 psect scp147,Typ_Lang,Attr_Rev,Edition,0,scpentry

 use defsfile


 ifdef	KANJI

 use "knjwrite.a"

 endc KANJI
 page
********************
* Macro definitions
*
 use <os9svc.m>


********************
* Definitions
*
BuffSize equ 500 I/O buffer size

*Pmode equ pct_stbtim auto-mode, 1.6uS pulse, negate prime
Pmode equ 0 auto-mode, 6.4uS pulse, negate prime
Irqmode equ pirq_enab enable irqs, ACK rising

* irq status flags

IrqAck_b equ 5 "pirq_ackint" bit # (acknowledge asserted)
IrqFalt_b equ 6 "pirq_faltint" bit # (fault asserted)

* printer status flags

StBusy_b equ 3 "pct_bsy" bit # (busy asserted)
StFalt_b equ 6 "pct_falt" bit # (fault asserted)
StAck_b equ 7 "pct_ack" bit # (acknowledge asserted)

Clr_irq equ pirq_faltint+pirq_ackint clear pending irqs

delayval equ 20 delay value for irq delay


********************
* Static Storage
*
 vsect
V_BufPtr ds.l 1 i/o buffer ptr
V_BufEnd ds.l 1 end of buffer ptr
V_NxtIn ds.l 1 next in ptr
V_NxtOut ds.l 1 next out ptr
V_IRQMsk ds.w 1 interrupt mask word
V_Ready ds.b 1 port ready flag
V_Data ds.b 1 data in buffer flag
V_Buffer ds.b BuffSize beginning of buffer
 ends


***************
* Entry Offset Table
*
scpentry dc.w SCInit initialization routine
 dc.w SCRead read routine
 ifndef	KANJI
 dc.w SCWrite write routine
 dc.w SCGetStt get status routine
 dc.w SCSetStt set status routine
 else
 dc.w WrtKanji kanji write routine
 dc.w KpGetStt get status routine
 dc.w KpSetStt set status routine
 endc KANJI
 dc.w SCTerm terminate routine
 dc.w 0 exception handler (0=none)
 pag
***************
* SCInit - initialize hardware
*
* Passed: (a1) = device descriptor ptr
*         (a2) = static storage ptr
*         (a4) = process descriptor ptr
*         (a6) = system global data ptr
*
* Returns:  Carry set, d1.w = error code if error
*
SCInit: lea.l V_Buffer(a2),a0 get buffer ptr
 move.l a0,V_BufPtr(a2) set buffer ptr
 move.l a0,V_NxtIn(a2) set next in ptr
 move.l a0,V_NxtOut(a2) set next out ptr
 lea.l BuffSize(a0),a0 get end of buffer ptr
 move.l a0,V_BufEnd(a2) set end ptr

 ifdef	KANJI
 bsr K_Set
 endc KANJI

* form interrupt mask value.
* NOTE: the IRQMasking is always performed "outside" of the Interrupt
* Service context, thus move.w V_IRQMsk(a2),sr is ok, as there will not
* be an accidental stack switch.
*
 move.b M$IRQLvl(a1),d2 get irq level
 asl.w #8,d2 shift into priority
 move.w sr,d0 get sr (for stack/trace flags)
 andi.w #IntEnab,d0 clear irq masks
 or.w d0,d2 form "current stack, irqs masked to this level" value
 move.w d2,V_IRQMsk(a2) save for future use

* add device to irq polling table

 move.b M$Vector(a1),d0 get vector number
 move.b M$Prior(a1),d1 get priority
 lea.l SCIRQ(pc),a0 addr of service routine
 movea.l V_PORT(a2),a3 get port addr
 OS9 F$IRQ Add to irq polling table
 bcs.s InitExit ..exit if error

* init printer port

 move.w sr,d6 save irq masks
 move.w V_IRQMsk(a2),sr mask interrupts
 move.b #Pmode,p_ctl(a3) set printer mode
 move.b M$IRQLvl(a1),d0 get irq h/w level
 andi.b #pirq_ilmask,d0 ensure valid range
 ori.b #Irqmode,d0 add irq operation mode
 move.b d0,p_irq(a3) enable irqs
 ori.b #Clr_irq,d0 add clear irq flags
 move.b d0,p_irq(a3) clear any pending irqs
 move.w d6,sr restore sr
 moveq.l #0,d1 flag no error
InitExit: rts return   

 pag
********************
* SCRead - read character from parallel port
*
* Passed: (a1) = path descriptor ptr
*         (a2) = static storage ptr
*         (a4) = process descriptor ptr
*         (a6) = system global data pointer
*
* Returns: Carry set, (d1.w) = E$EOF
*
SCRead: move.w #E$EOF,d1 return "End of File" error
 ori.b #Carry,ccr set carry
 rts


********************
* SCGetStt - get status routines
*
* Passed: (a1) = path descriptor ptr
*         (a2) = static storage ptr
*         (a4) = process descriptor ptr
*         (a5) = caller's register stack ptr
*         (a6) = system global data pointer
*         d0.w = status call function code
*
* Returns:  Carry set, d1.w = E$UnkSvc
*
SCGetStt: move.w #E$UnkSvc,d1 return "Unknown Service Request"
 ori.b #Carry,ccr set carry
 rts return


********************
* SCSetStt - set status routines
*
* Passed: (a1) = path descriptor ptr
*         (a2) = static storage ptr
*         (a4) = process descriptor ptr
*         (a5) = caller's register stack ptr
*         (a6) = system global data pointer
*         d0.w = status call function code
*
* Returns:  Carry set, d1.w = E$UnkSvc
*
SCSetStt: equ SCGetStt same as get status
 pag
********************
* SCTerm - Terminate processing.
*
* Wait for all output to be sent, then terminate the device.
*
* Passed: (a1) = device descriptor ptr
*         (a2) = static storage
*         (a4) = process descriptor ptr
*         (a6) = system global data ptr
*
* Returns: none
*
SCT_A bsr.s SCSleep wait for port ready

SCTerm: movea.l V_PORT(a2),a3 get port address
 move.w P$ID(a4),d0 get process id
 move.w d0,V_BUSY(a2) mark user of port...
 move.w d0,V_LPRC(a2) ..in case of signals
 move.w sr,d6 save interrupt masks
 move.w V_IRQMsk(a2),sr mask irqs
 move.b V_Data(a2),d1 is buffer empty?
 bne.s SCT_A ..No; Wait
 move.w d6,sr restore sr
 
SCTerm_B tst.b V_Ready(a2) now, wait for final irq to set port ready
 bne.s SCTerm_B

* clear device irq control

 move.b #Clr_irq,p_irq(a3) clear/disable all irqs
 move.b M$Vector(a1),d0 get vector number
 suba.l a0,a0 set zero address
 OS9 F$IRQ remove port from polling tbl
 rts
 pag
***************
* SCSleep - Sleep until port is ready
*
* Passed: d6.w = saved SR
*         (a1) = path descriptor ptr
*         (a2) = static storage ptr
*         (a4) = current process descriptor ptr
*         (a6) = system global data pointer
*
* Returns: Carry = Set if signal error
*
* Destroys:
*
SCSleep: move.w V_BUSY(a2),V_WAKE(a2) set wake-up call
 move.w d6,sr restore sr
 move.l d0,-(a7) save (d0)
 moveq.l #0,d0 wait for signal
 OS9svc F$Sleep call sleep directly
 move.w P$Signal(a4),d0 signal waiting?
 beq.s SCSleep_B ..No; check status 
 cmpi.w #S$Deadly,P$Signal(a4) deadly signal?
 blo.s SCSleep_C ..Yes; return error
SCSleep_B: btst.b #Condemn,P$State(a4) has process died?
 bne.s SCSleep_C ..yes; exit
 move.l (a7)+,d0 restore d0, clear carry
 rts return

SCSleep_C: move.l (a7)+,d0 restore d0
 ori.b #Carry,ccr set carry to flag signal/death
 rts
 pag
***************
* SCWrite - Write one byte to parallel port
*
* Passed: (a1) = path descriptor ptr
*         (a2) = static storage ptr
*         (a4) = process descriptor ptr
*         (a5) = caller's register stack ptr
*         (a6) = system global data pointer
*         d0.b = char to write to port
*
* Returns: Carry set, d1.w = error code if error
*
SCW_A bsr.s SCSleep wait for port
 bcs.s SCW_Z abort if error

 ifdef	KANJI
Write:
 endc KANJI

SCWrite: movea.l V_PORT(a2),a3 get port ptr
 move.w sr,d6 save interrupt masks
 move.w V_IRQMsk(a2),sr mask irqs
 tst.b V_Ready(a2) is port ready?
 beq.s SCW_G branch if so
 tst.b V_Data(a2) is data in buffer?
 bne.s SCW_E branch if so
 move.b p_status(a3),d1 get port status

* normally, the Busy line will be asserted when the printer is
* Busy, Deselected or in a Fault condition

 btst.l #StBusy_b,d1 is printer busy ?
 bne.s SCW_E ..yes; simply buffer char
 btst.l #StAck_b,d1 what about the state of ACK ?
 beq.s SCW_F ..if deasserted, then printer ready for data
SCW_E bsr.s BuffIn put byte in buffer
 bcs.s SCW_A branch if not successful
 move.w d6,sr restore sr
 moveq.l #0,d1 clear Carry
 rts

SCW_F move.b p_irq(a3),d1 get printer irq status
 bclr.l #IrqFalt_b,d1 keep fault status alive
 move.b d1,p_irq(a3) clear interrupt
SCW_G move.b #1,V_Ready(a2) set port not ready
 move.w d6,sr restore sr
 move.b d0,p_data(a3) write byte
SCW_Z rts
 pag
********************
* BuffIn - put character into buffer
*
* Passed:  (a2) = static storage ptr
*          d0.b = character to buffer
*
* Returns:  Carry set if buffer full
*
* Destroys: a0
*
BuffIn: movea.l V_NxtIn(a2),a0 get next in ptr
 tst.b V_Data(a2) already data in buffer?
 beq.s BufI_A branch if not
 cmpa.l V_NxtOut(a2),a0 is buffer full?
 bne.s BufI_B branch if not
 ori.b #Carry,ccr set Carry
 rts

BufI_A move.b #1,V_Data(a2) set data in buffer flag

BufI_B move.b d0,(a0)+ put byte in buffer
 cmpa.l V_BufEnd(a2),a0 end of buffer?
 bcs.s BufI_C branch if not
 movea.l V_BufPtr(a2),a0 get buffer ptr
BufI_C move.l a0,V_NxtIn(a2) update next in ptr
 rts


********************
* BuffOut - read character from buffer
*
* Passed:  (a2) = static storage ptr
*
* Returns:  Carry set if buffer empty, else
*           d0.b = character to buffer
*
* Destroys: a0
*
BuffOut: tst.b V_Data(a2) is there data in buffer?
 bne.s BufO_A branch if so
 ori.b #Carry,ccr set Carry
 rts

BufO_A movea.l V_NxtOut(a2),a0 get next out ptr
 move.b (a0)+,d0 get byte from buffer
 cmpa.l V_BufEnd(a2),a0 end of buffer?
 bcs.s BufO_B branch if not
 movea.l V_BufPtr(a2),a0 reset next out ptr
BufO_B move.l a0,V_NxtOut(a2) update next out ptr
 cmpa.l V_NxtIn(a2),a0 is buffer empty?
 bne.s BufO_C branch if not
 clr.b V_Data(a2) clear data in buffer flag
BufO_C moveq.l #0,d1 clear Carry
 rts
 pag
***************
* SCIRQ - process parallel port interrupt
*
* Passed: (a2) = static storage
*         (a3) = port address
*         (a6) = system global data pointer
*
* Returns:  Carry = Set if interrupt not serviced
*
* The kernel only preserves d0, d1, a0, a2, a3 and a6.
* All other registers used must be preserved by this routine.
*
SCIRQ: move.b p_irq(a3),d0 get irq status
 bmi.s SCIRQ_0 ..yes it's our irq; continue
 ori.b #Carry,ccr no, was not
 rts

SCIRQ_00 move.b p_irq(a3),d0 get irq status

SCIRQ_0: bclr.l #IrqFalt_b,d0 was fault asserted ?
 bne.s SCIRQ_G ..yes; bra
 move.b d0,p_irq(a3) reset ack irq
 bsr.s BuffOut get byte from buffer
 bcs.s SCIRQ_A branch if empty
 move.b d0,p_data(a3) write it
 move.w #delayval,d0 delay a little....
delay dbra d0,delay ...for status to settle
 bsr.s SCIRQ_C check for waiting process (and delay a bit)
 btst.l #StBusy_b,d0 is busy line asserted ?
 bne.s SCIRQ_X ..yes; leave next char till when ready
 btst.l #StAck_b,d0 is ACK asserted ?
 beq.s SCIRQ_00 ..no; printer ready for data now, so keep on pumping
SCIRQ_X moveq.l #0,d1 clear carry
 rts

SCIRQ_A clr.b V_Ready(a2) set port ready

SCIRQ_C move.w V_WAKE(a2),d0 is there waiting process?
 beq.s SCIRQ_D ..no; return
 clr.w V_WAKE(a2) mark signal sent
 moveq.l #S$Wake,d1 (wake up)
 OS9svc F$Send call send directly
SCIRQ_D move.b p_status(a3),d0 return port ready status
 rts

SCIRQ_G bclr.l #IrqAck_b,d0 keep ack status
 bset.l #IrqFalt_b,d0 reset fault irq
 move.b d0,p_irq(a3) clear fault irq
 bra.s SCIRQ_C wakeup waiter

 ends

