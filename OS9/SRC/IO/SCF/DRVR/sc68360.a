 nam SC68360
 ttl Interrupt-Driven driver for M68360 on-board SCC's

* Copyright 1993, 1994 by Microware Systems Corporation
* Reproduced Under License

* This source code is the proprietary confidential property of
* Microware Systems Corporation, and is provided to licensee
* solely for documentation and educational purposes. Reproduction,
* publication, or distribution in any form to any party other than
* the licensee is strictly prohibited.

***************
* Edition History

* #   date      Comments                                        by
* -- -------- ------------------------------------------------- ---
* 01 93/10/25 Converted from 68302 driver						arb
* 02 94/02/02 Reworked for MWOS structure.						wwb
*             ---- OS-9/68K V3.0.1 Release ----
* 03 97/01/06 Corrected XOFF bugs. Corrected GSMR_H offset bug. jam
* 04 97/02/20 Added 57.6k and 115.2k baud rates.                jam
* 05 99/07/03 CF3044 restore registers on ErrNtRdy error        srm
* 06 01/09/17 CF11072 Fixed registers in IRQ section            
*             added new baud rates to BaudTable                 hjt
*
Edition equ 6 current Edition number

Typ_Lang set (Drivr<<8)+Objct
Attr_Rev set ((ReEnt+SupStat)<<8)+0

 psect sc68360,Typ_Lang,Attr_Rev,Edition,0,M68360Ent

 use defsfile

 pag
********************
* Macro Definitions
*
 use <os9svc.m>

 pag
********************
* Definitions
*
Signbit set 7

 ifdef IPBUF
 ifeq IPBUF-1
 fail must specify a value for IPBUF
 endc
InpSiz set IPBUF set user defined buffer size
 else
InpSiz set 80 input buffer size
 endc IPBUF
 ifdef OPBUF
 ifeq OPBUF-1
 fail must specify a value for OPBUF
 endc
OutSiz set OPBUF set user defined buffer size
 else
OutSiz set 140 output buffer size
 endc OPBUF
MinBuff equ 20 Send XON when buffer contains this few
MaxBuff equ InpSiz-MinBuff Send XOFF when buffer contains this many

 pag
**********
* Static storage offsets
*
 vsect
InFill ds.l 1 input buffer next-in ptr
InEmpty ds.l 1 input buffer next-out ptr
InEnd ds.l 1 end of input buffer
OutFill ds.l 1 output buffer next-in ptr
OutEmpty ds.l 1 output buffer next-out ptr
OutEnd ds.l 1 output buffer end of buffer pointer
DPAddr: ds.l 1 base address of dual port memory
ConReg:	ds.l 1 base address of the SCC's configuration Registers
IVal:	ds.l 1 Value to reset an interrupt in the ISR or unmask one in the IMR
InCount ds.w 1 # of chars in input buffer
OutCount ds.w 1 # of chars in output buffer
IRQMask ds.w 1 Interrupt mask word
SigPrc ds.w 3 signal on data ready process (pid, signal, path)
 ifdef DCDSIG
DCDPrc ds.w 3 signal for DCD transitions process (pid, sig, path)
	endc
BufOff:	ds.w 1 Offset to RX buffer in 68360 dual port RAM
StopTX	ds.w 1 Value to write to the Config register for a STOPTX command
StartTX	ds.w 1 Value to write to the Config register for a RESTTX command
InitCPM ds.w 1 Value to init CPM channel
CHnum	ds.b	1	SCC Channel #
inbuf	ds.b	1   temp input buffer
otbuf	ds.b	1   temp output buffer
BaudRate ds.b 1 baud rate value
Parity ds.b 1 current parity value
InHalt ds.b 1 input halted flag (non-zero if XON has been Sent)
OutHalt ds.b 1 output IRQ's disabled when non-Zero
InpBuf ds.b InpSiz input buffer
OutBuf ds.b OutSiz output buffer
 ends

* OutHalt bit numbers (causes of disabled output IRQ)

H_XOFF equ 0 V_XOFF has been received; awaiting V_XON
H_Empty equ 1 output buffer is empty


********************
* Entry jump table
*
M68360Ent: dc.w Init
 dc.w Read
 dc.w Write
 dc.w GetStat
 dc.w SetStat
 dc.w Term
 dc.w 0 Exception handler entry (0=none)


 ttl INTERRUPT-DRIVEN 68360 on-board SCC device routines
 pag
***************
* Init
*   Initialize (Terminal) Port of M68360
*
* Passed:  (a1) = device descriptor address
*          (a2) = static storage address
*          (a4) = process descriptor ptr
*          (a6) = system global data ptr
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
* Destroys: (may destroy d0-d7, a0-a5)
*
* register usage notes
* a0 = scratch
* a3 = dual port ram
* a5 = parameter ram
*
Init:
 movem.l d0/d2/a0/a3-a5,-(sp)

* initialize static storage
 move.b #(1<<H_Empty),OutHalt(a2) output IRQ's disabled; buffer empty
 move.w #0,OutCount(a2) no chars in output buffer
 move.w #0,InCount(a2) no chars in input buffer
 lea.l InpBuf(a2),a0 init buffer pointers
 move.l a0,InFill(a2) 
 move.l a0,InEmpty(a2)
 lea.l InpSiz(a0),a0 figure size of buffer
 move.l a0,InEnd(a2) mark end of input buffer
 lea.l OutBuf(a2),a0 point to start of output buf
 move.l a0,OutFill(a2) init output buff pointers
 move.l a0,OutEmpty(a2)
 lea.l OutSiz(a0),a0 figure size of out buff
 move.l a0,OutEnd(a2) mark end of output buffer
 move.b M$IRQLvl(a1),d2 get irq level
 asl.w #8,d2 shift into priority
 clr.l d0
 move.w	sr,d0
 andi.w #$f8ff,d0			remove any irq bits
 or.w	d2,d0
 move.w d0,IRQMask(a2) save for future use

* initialize vector register and add device to system polling table
 move.l	V_PORT(a2),a3	get port address
 move.b M$Vector(a1),d0 get descriptor's vector
 move.b M$Prior(a1),d1 get priority
 lea.l M68360IRQ(pc),a0 address of interrupt service routine
 OS9 F$IRQ Add to IRQ polling table
 bcs.s InitExit ..exit if error

* initialize port configuration
 bsr.s SetSCCInfo		get base addr of param ram & config regs

 bsr InitParams Init Param RAM & Control regs

 move.b PD_BAU-PD_OPT+M$DTyp(a1),d0 get baud rate
 move.b d0,BaudRate(a2) store for later
 bsr BaudCalc set baud rate
 bcs.s InitExit ..exit if error
 move.b PD_PAR-PD_OPT+M$DTyp(a1),d0 get stop bits, bit/char & parity
 move.b d0,Parity(a2) store for later
 bsr BitPar set stop bits, parity, & bits per char
*! Finally enable the transmitter and receiver
 move.l ConReg(a2),a0
 move.l	#$00000060,GSMR_H(a0) set Transmit FIFO for 1 byte
 move.l	#$00028004,GSMR_L(a0)	; enable transmitter and receiver

 ifndef POLLED_IO
 move.l CIMR(a3),d0
 or.l IVal(a2),d0 set bit to enable ints for this SCC
 move.l d0,CIMR(a3)
 move.w	#$ffff,SCCE(a0)			; clear any pending irqs
 move.w	#RXEV,SCCM(a0)				; enable rx irq's
 endc

 move.l	#$00028034,GSMR_L(a0)	; enable transmitter and receiver
InitExit: 
 clr.l	d0
 movem.l (sp)+,d0/d2/a0/a3-a5
 rts

***************
* SetSCC Info
*   Set up static storage to reflect this SCC
*
* Passed:  	(a2) = static storage address -> uses V_PORT(a2)
*
* Returns:	none
* Error Return:  (cc) = carry set
*                d1.w = error code
*
* Destroys: none
*
SetSCCInfo:
 movea.l V_PORT(a2),a0 I/O port address (actually parameter RAM address)

* generate the base address of the 68360 registers
 move.l a0,d0 get port address
 andi.l #$fffff000,d0 mask off parameter RAM offset
 move.l d0,DPAddr(a2) set DPRAM base
 movea.l d0,a3 set a3=DPRAM base
* work out which of the 4 SCC's we're accessing
 move.l a0,d0 get port address again
 lsr.l #8,d0 get parameter RAM offset to be 0-3
 andi.l #3,d0
 move.b	d0,CHnum(a2)		save for later use
* get the value for enabling an interrupt via the Interrupt Mask
* Register (CIMR) and resetting an interrupt via the In Service
* Register (CISR)
 move.l	#$40000000,d1
 lsr.l	d0,d1
 move.l d1,IVal(a2)

* now figure out which config registers to use
 lea.l	GSMR_L1(a3),a0		point to scc1 config registers
 lsl.l	#5,d0				32 bytes /scc for registers
 add.l	d0,a0
 move.l a0,ConReg(a2) get the offset to the config registers

 lsl.l	#1,d0				64 bytes / scc for bd's
* get offset to the RX buffer in the dual ported RAM
* N.B. The TX buffer is RX buffer address plus 1
 move.w d0,BufOff(a2)

* finally set up the values for 'STOPTX' and 'STARTTX' commands for 
* the config register
 clr.l	d0
 move.b	CHnum(a2),d0
 lea.l	CPMCH(pc),a0
 move.b	(a0,d0),d0
 move.l d0,-(sp)
 ori.w #RESTTX+FLG,d0
 move.w d0,StartTX(a2)
 move.l (sp),d0
 ori.w #STOPTX+FLG,d0
 move.w d0,StopTX(a2)
 move.l (sp)+,d0
 ori.w #INITXRX+FLG,d0
 move.w d0,InitCPM(a2)
 rts

* Comm Processor Channel #
CPMCH:			
	dc.b	0		scc1
	dc.b	$40		scc2
	dc.b	$80		scc3
	dc.b	$c0		scc4


*********************
* ParamSet - set up the SCC Parameter RAM values
InitParams:
 movem.l d0/a0/a3/a5,-(sp)
 move.l	DPAddr(a2),a3			point to dual port ram
 move.l	V_PORT(a2),a5
 move.w BufOff(a2),d0
 move.w d0,RBASE(a5)			first 8 bytes of dp ram is rxbd
 lea.l	inbuf(a2),a0
 move.l	a0,4(a3,d0.w)				*! point to rx buffer 
 clr.w	2(a3,d0.w)			
 addq.w	#8,d0 
 move.w d0,TBASE(a5)		next 8 bytes of dp ram is txbd
 move.l	OutEmpty(a2),d1
 move.l	d1,4(a3,d0.w)				*! point to out buffer
 move.w	#1,2(a3,d0.w) 
 	move.b	#FC_DMA+$10,RFCR(a5)	
 	move.b	#FC_DMA+$10,TFCR(a5)	
 	move.w	#1,MRBLR(a5)		; max. bytes per Buffer (1)
*** Execute the INIT RX & TX Parameters for SCC3
	move.w	InitCPM(a2),d0
	move.w	d0,CR(a3)	; initialize transmit and receive parameter
*							; in the parameter ram of scc3
*** Set up UART Specific Parameters
	move.w	#4,MAXIDL(a5)	; set idle timeout to inf
	move.w	#$0001,BRKCR(a5)	; stop trans issues 1 break chara.
	clr.w	PAREC(a5)		; clear error counters
	clr.w	FRMEC(a5)
	clr.w	NOSEC(a5)
	clr.w	BRKEC(a5)
	clr.w	UADDR1(a5)		; no multidrop address
	clr.w	UADDR2(a5)
	clr.w	TOSEQ(a5)		; No CTRL Char to be sent
	move.w	#$8000,CHARACTER1(a5)	; No more control Characters
	move.w	#$8000,CHARACTER2(a5)	; No more control Characters
	move.w	#$8000,CHARACTER3(a5)	; No more control Characters
	move.w	#$8000,CHARACTER4(a5)	; No more control Characters
	move.w	#$8000,CHARACTER5(a5)	; No more control Characters
	move.w	#$8000,CHARACTER6(a5)	; No more control Characters
	move.w	#$8000,CHARACTER7(a5)	; No more control Characters
	move.w	#$8000,CHARACTER8(a5)	; No more control Characters
	move.w	#$8000,RCCM(a5)	; No masking of received Ctl Chars
 
* SCC Parameter RAM
 move.w BufOff(a2),d0
 ifdef POLLED_IO
 move.w #RXE+RXW,(a3,d0.w)			*! Rx Buffer 0 available to CP, last buffer
 move.w #TXW,8(a3,d0.w)				*! Tx Buffer 0 is last buffer
 else
 move.w #RXE+RXI+RXW,(a3,d0.w)		*! As above with interrupt generation
 move.w #TXI+TXW,8(a3,d0.w)			*! Ditto
 endc ifdef POLLED_IO
 movem.l (sp)+,d0/a0/a3/a5
 rts
 pag
********************
* BaudCalc:  initialize baud rate of device
*
* Passed:  d0.b = OS-9 baud rate code
*          d1.w = Offset to SCC configuration Registers
*          (a2) = static storage ptr
*          (a3) = Base Address of dp ram
*          (a6) = system global data ptr
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
BaudCalc: movem.l d0-d1/a0/a3/a5,-(sp) save reg
 move.l	DPAddr(a2),a3			point to dual port ram
 andi.w #$00ff,d0 mask out all except baud rate
 lsl.w #2,d0 multiple by 4 for lword offset
 cmpi.w #MaxBaud,d0 legal baud rate ?
 bge.s BadMode ..no; return error 
 lea.l BaudTable(pcr),a0 get table address
 move.l (a0,d0.w),d1 get baud rate value for chip
 bmi.s BadMode ..exit if unsupported rate
 lea	BRGC1(a3),a5
 clr.l d0
 move.b	CHnum(a2),d0
 move.l d1,(a5,d0*4) set baud rate in device
 move.l	d0,d1
 lsl.l	#3,d1
 or.b	d0,d1
 mulu.l	#8,d0
 lsl.l	d0,d1		d1 now has routing bits for SI clock Route Register
 or.l	d1,SICR(a3) route baud clock to scc
 clr.l d0
 movem.l (sp)+,d0-d1/a0/a3/a5 restore register
 rts return (carry clear)

* here if illegal or unsupported baud-rate

BadMode: movem.l (sp)+,d0-d1/a0/a3/a5

BadMode10: move.w #E$BMode,d1 get error code
 ori.b #Carry,ccr set the carry
 rts return

* Baud rate table:

BaudTable:
 dc.l $10f41	    0 - 50 baud 
 dc.l $10a2b	    1 - 75 baud unavailable
 dc.l $106ef	    2 - 110 baud unavailable
 dc.l $105ac	    3 - 134.5 baud unavailable
 dc.l $10515	    4 - 150 baud
 dc.l $10289        5 - 300 baud
 dc.l $11456	    6 - 600 baud
 dc.l $10a2a	    7 - 1200 baud
 dc.l $106c8	    8 - 1800 baud unavailable
 dc.l $1061a	    9 - 2000 baud unavailable
 dc.l $10514	    A - 2400 baud
 dc.l $10364	    B - 3600 baud unavailable
 dc.l $10288	    C - 4800 baud
 dc.l $101b2	    D - 7200 baud unavailable
 dc.l $10144 	    E - 9600 baud
 dc.l $100a0 	    F - 19.2k baud
 dc.l $10050 	   10 - 38.4k baud
 dc.l $10034       11 - 56.0k baud
 dc.l $1001a       12 - 64.0k baud
 dc.l $ffffffff    13 - 31250 baud
 dc.l $ffffffff    14 - 31250 baud
 dc.l $ffffffff    15 - 57.6k baud
 dc.l $ffffffff    16 - 115.2k baud
 dc.l $ffffffff    17 - 230.4k baud
 dc.l $ffffffff    18 - 460.8k baud
 dc.l $ffffffff    19 - 921.6k baud
 dc.l $ffffffff    1a - 76.8k baud
 dc.l $ffffffff    1b - 153.6k baud
 dc.l $ffffffff    1c - 307.2k baud
 dc.l $ffffffff    1d - 614.4k baud
 dc.l $ffffffff    1e - 1228.8k baud 

MaxBaud: equ *-BaudTable
   
 pag
******************************
* BitPar:  set bits/character, # stop bits, parity mode
*
* Passed:  d0.b = device configuration
*                 bits 1,0:  0 = no parity
*                            1 = odd parity
*                            3 = even parity
*                 bits 3,2:  0 = 8 bit data
*                            1 = 7 bit data
*                            2 = 6 bit data
*                            3 = 5 bit data
*                 bits 5,4:  0 = 1 stop bit
*                            1 = 1.5 stop bits
*                            2 = 2 stop bits
*          d1.w = Offset to SCC configuration Registers
*          (a2) = static storage pointer
*          (a3) = 68360 base address
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
BitPar: movem.l d0-d3/a0,-(sp) save register
 moveq.l #0,d2 sweep d2
 andi.w #$00ff,d0 clear high end of word
 move.w d0,d1 copy parity value
 andi.w #3,d1 isolate parity code
 lsl.w #1,d1 convert to word offset
 lea.l TabPar(pc),a0 point at parity mode table
 move.w (a0,d1.w),d2 get parity code
 bmi.s BadMode ..exit if illegal value
 move.w d0,d1 get data bit size
 lsr #2,d1 makelegal index
 andi.w #3,d1 make legal index
 lsl.w #1,d1 convert to word offset
 lea.l TabBC(pc),a0 point at bits/char table
 move.w (a0,d1.w),d3 get bits/char
 bmi BadMode
 or.w d3,d2
 move.w d0,d1 get stop bit value
 lsr.w #4,d1 make index value
 andi.w #3,d1 make legal index
 lsl.w #1,d1 convert to word offset
 lea.l TabSB(pc),a0 point at stop bit table
 move.w (a0,d1.w),d3 get stop bit value
 bmi BadMode ..exit if illegal value 
 or.w d3,d2
 or.w	#$8000,d2		set flow control
 move.l	ConReg(a2),a0
 move.w d2,PSMR(a0)
BitPar20: 
 clr.l	d0
 movem.l (sp)+,d0-d3/a0 restore a0
 rts return (carry clear)
 
TabPar:
 dc.w $00 no parity
 dc.w $10 odd parity (only need to enable parity)
 dc.w $ffff illegal value
 dc.w $1a  even parity
 
TabBC:
 dc.w $3000 8 bits per char
 dc.w $2000 7 bits per char
 dc.w $1000 6 bits per char
 dc.w $0000 5 bits per char
 
TabSB:
 dc.w $00 1 stop bit
 dc.w $ffff 1.5 stop bits (for 5 bit data) - illegal for 68360 SCCs
 dc.w $4000 2 stop bits
 dc.w $ffff illegal value
 
 pag
********************
* Read:s  Return one byte of input from a 68360 SCC
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = current process descriptor
*          (a6) = system global ptr
*
* Returns:  (d0.b) = input char
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
* Destroys: a0
*
 ifdef POLLED_IO
Read:
 movem.l d1/a3-a5,-(sp)
 movea.l V_PORT(a2),a5
 move.l	DPAddr(a2),a3
 clr.l d1
 move.w	 RBASE(a5),d1
 add.l	d1,a3
Read10 move.w (a3),d1 RX char available ?
 btst #15,d1
 bne.s Read10 ..no; wait for one
 move.l	4(a3),a4
 move.b	(a4),d0
 move.w #RXE+RXW,(a3) Denote buffer now available to CP
 movem.l (sp)+,d1/a3-a5
Read90 rts

 else 	*! Interrupt driven Read routine

Read00 move.w V_BUSY(a2),V_WAKE(a2) arrange wake up signal
 move.w (sp)+,sr restore IRQs
 movem.l (sp)+,d1-d2/a3
 bsr M68360Sleep

Read: movem.l d1-d2/a3,-(sp)
 movea.l DPAddr(a2),a3 get 68360 base address
 tst.b InHalt(a2) is input halted?
 ble.s Read_a branch if not
 cmpi.w #MinBuff,InCount(a2) buffer mostly emptied?
 bhi.s Read_a ..No; continue
 movea.l V_PORT(a2),a0 get parameter RAM address
 clr.w d1
 move.b V_XON(a2),d1 get X-ON char
 move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 clr.b InHalt(a2) clean up XOFF flag
 or.w #CCE+CCREA+CCI,d1
 move.w d1,CHARACTER8(a0) Write Char to buffer
 ori.b #Sign,d1 set Sign bit
 move.b d1,InHalt(a2) flag input resume
 move.l ConReg(a2),a0
 or.w #RXEV,SCCM(a0) re-enable Receive Interrupts
 move.w (sp)+,sr unmask IRQs
Read_a tst.w SigPrc(a2) a process waiting for device?
 bne.s ReadErrNtRdy ..Yes; return dormant terminal error
 move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 tst.w InCount(a2) any data?
 beq.s Read00 branch if not
 move.w (sp)+,sr unmask IRQs
 movea.l InEmpty(a2),a0 point to next char
 move.b (a0)+,d0 get character
 subq.w #1,InCount(a2) dec buffer size by one
 cmpa.l InEnd(a2),a0 at end of buffer?
 blo.s Read_b branch if not
 lea.l InpBuf(a2),a0 point to start of buffer
Read_b move.l a0,InEmpty(a2) update buffer pointer
 movem.l (sp)+,d1-d2/a3
 move.b V_ERR(a2),PD_ERR(a1) copy I/O status to PD
 beq.s Read90 .. no error
 clr.b V_ERR(a2)
 move.w #E$Read,d1
 ori.b #Carry,ccr
Read90 rts
ReadErrNtRdy: movem.l (sp)+,d1-d2/a3 restore register
 endc ifdef POLLED_IO
ErrNtRdy: move.w #E$NotRdy,d1
 ori.b #Carry,ccr return Carry set
 rts

 pag
********************
* M68360Sleep:  Sleep until interrupt occurs
*
* Passed:  (a2) = driver global storage
*          (a4) = current process descriptor ptr
*          (a6) = system global data ptr
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code (signal)
*
* Destroys:  possibly PC
*
M68360Sleep:
 move.l d0,-(sp) save reg
 moveq.l #0,d0 sleep indefinately
 OS9svc F$Sleep wait for interrupt (*only works with Sleep 0*)
 move.w P$Signal(a4),d1 signal present?
 beq.s ACSL90 ..no; return
 cmpi.w #S$Deadly,d1 Deadly signal?
 blo.s ACSLER ..yes; return error
ACSL90 btst.b #Condemn,P$State(a4) has process died?
 bne.s ACSLER ..Yes; return error
 move.l (sp)+,d0 restore register, clear carry
 rts

ACSLER addq.l #8,sp Exit to caller's caller
 ori.b #Carry,ccr return Carry set

 rts

 pag
********************
* Write
*   Output one character via 68360 SCC
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = current process descriptor ptr
*          (a6) = system global data ptr
*          d0.b = char to write
*
* Returns:  nothing
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
 ifdef POLLED_IO
Write: movem.l d1/a3-a5,-(sp)
 move.l	 V_PORT(a2),a5
 movea.l DPAddr(a2),a3 get 68360 base address
 clr.l d1
 move.w	 TBASE(a5),d1		get offset to buffers
 add.l	d1,a3
Write00 move.w (a3),d1	Get Transmit Buffer Info
 btst #15,d1 Can we write to buffer?
 bne.s Write00 ..no
 move.l	4(a3),a4
 move.b d0,(a4) write data to TX buffer
 move.w #TXR+TXW,(a3) .. and transmit it
 movem.l (sp)+,d1/a3-a5
 rts return

 else

Write00 move.w V_BUSY(a2),V_WAKE(a2) arrange wake up signal
 move.w (sp)+,sr restore IRQs
 bsr.s M68360Sleep

Write: 
 move.w sr,-(sp)
 move.w IRQMask(a2),sr mask IRQs
 cmpi.w #OutSiz,OutCount(a2) room for more data?
 bhs.s Write00 
 bclr #H_Empty,OutHalt(a2) was output buffer empty?
 beq.s Write20 just exit if not
 tst.b OutHalt(a2) still halted due to other cause?
 bne.s Write20 don't enable IRQ if so
 move.l	 V_PORT(a2),a3    get base address of 68360
 move.w  TBASE(a3),d1
 move.l DPAddr(a2),a3 point to DP ram
 add.w	d1,a3		a3 now points to TX bd
 lea	otbuf(a2),a0
 move.l a0,4(a3) put TX buffer in bd
 move.b d0,(a0) store char
 move.w	#1,2(a3)
 move.w #TXR+TXW+TXI,(a3) .. and send the char
 bra.s Write80
Write20
 addq.w #1,OutCount(a2) increment byte count
 movea.l OutFill(a2),a0 point to next char location
 move.b d0,(a0)+ store char and inc pointer
 cmpa.l OutEnd(a2),a0 end of buffer?
 blo.s Write50 branch if not
 lea.l OutBuf(a2),a0 point to start of buffer
Write50 move.l a0,OutFill(a2) update buffer pointer
 btst #H_XOFF,OutHalt(a2) halted due to XOFF?
 bne.s Write80			   yes, don't restart output
 move.l ConReg(a2),a3 get offset to config registers
 ori.w #TXEV,SCCM(a3) enable TX interrupts
Write80 
 move.w (sp)+,sr unmask IRQs 
 moveq.l #0,d1 clear carry
 rts

 endc ifdef POLLED_IO

 pag
********************
* GetStat:  get device status
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = process descriptor
*          (a5) = caller's register stack ptr
*          (a6) = system global data ptr
*          d0.w = status call function code
*
* Returns:  varies with function code
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
GetStat:

* return data available count
*
 cmpi.w #SS_Ready,d0 Ready status?
 bne.s GetSta10 ..no
 clr.w R$d1(a5) sweep high word of register
 move.w InCount(a2),R$d1+2(a5) return input char count to caller
 beq ErrNtRdy ..No data; return not ready error
 rts (Carry clear)

* return eof status
*
GetSta10 cmpi.w #SS_EOF,d0 End of file?
 beq.s GetSta99 ..yes; return (Carry clear)

* check for "get options"
*
 cmpi.w #SS_Opt,d0 get options call ?
 bne.s Unknown ..no; return error for unknown request

* update path descriptor for currently active baud, parity
*
 move.b BaudRate(a2),PD_BAU(a1) set currently active baud rate
 move.b Parity(a2),PD_PAR(a1) set currently active comms mode
 rts (carry clear)

* return error for unknown service requests
*
Unknown: move.w #E$UnkSvc,d1 Unknown service code
 ori.b #Carry,ccr return Carry set
GetSta99: rts

 pag
********************
* SetStat:  set device status
*
* Passed:  (a1) = Path Descriptor
*          (a2) = Static Storage address
*          (a4) = process descriptor
*          (a5) = caller's register stack ptr
*          (a6) = system global data ptr
*          d0.w = status call function code
*
* Returns:  varies with function code
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
SetStat:
 ifndef POLLED_IO
* signal process on data available
*
 cmpi.w #SS_SSig,d0 signal process when ready?
 bne.s SetSta_A ..No
 tst.w SigPrc(a2) somebody already waiting?
 bne ErrNtRdy ..Yes; error
 move.w PD_CPR(a1),d0 get caller's process ID
 move.w R$d2+2(a5),d1 get signal code
 move.w sr,-(sp) save IRQ status
 move.w IRQMask(a2),sr disable IRQs
 tst.w InCount(a2) any Data available?
 bne.s SetSta10 yes, signal Data ready
 move.w PD_PD(a1),d2 get associated path #
 movem.w d0-d2,SigPrc(a2) save process id, signal, path #
 move.l ConReg(a2),a3 get offset to config registers
 ori.w #RXEV,SCCM(a3) ensure Receive Interrupts enabled
 move.w (sp)+,sr unmask IRQs
 moveq.l #0,d1 clear carry

 rts

SetSta10 move.w (sp)+,sr restore IRQ status
 OS9svc F$Send
 moveq.l #0,d1
 rts

* release all signal conditions
*
SetSta_A cmpi.w #SS_Relea,d0 Release Device?
 bne.s SetSta_B bra if not
 move.w PD_CPR(a1),d0 get process id
 move.w PD_PD(a1),d2 get associated path #
 lea.l SigPrc(a2),a3 check SigPrc

 ifdef DCDSIG
 bsr.s ClearSig
 lea.l DCDPrc(a2),a3 now check DCDPrc
 endc DCDSIG

ClearSig cmp.w (a3),d0 is signal for this process ?
 bne.s ClearSig20 ..no; exit
 cmp.w 4(a3),d2 does it concern this path ?
 bne.s ClearSig20 ..no; exit
 clr.w (a3) clear down signal condition
ClearSig20 moveq.l #0,d1 flag no error
 rts return

 ifdef DCDSIG

* signal on DCD transitions
*
SetSta_B cmpi.w #SS_DCOn,d0 signal on carrier detect?
 bne.s SetSta_C .. no
 move.w PD_CPR(a1),d0 get process id
 move.w R$d2+2(a5),d1 get signal code
 move.w PD_PD(a1),d2 get associated path #
 move.l ConReg(a2),a3 get config registers offset
 move.w sr,-(sp)
 move.w IRQMask(a2),sr
 move.w SCCS(a3),d3 get status register contents

 btst #b_DCD,d3 DCD already asserted?
 bne SetSta30 .. yes, send the signal
 bra.s SetSta20

SetSta_C cmpi.w #SS_DCOff,d0 on loss of carrier?
 bne.s SetSta_D .. no
 move.w PD_CPR(a1),d0 get process id
 move.w R$d2+2(a5),d1 get signal code
 move.w PD_PD(a1),d2 get associated path #
 move.l ConReg(a2),a3 get config registers offset
 move.w sr,-(sp)
 move.w IRQMask(a2),sr
 move.w SCCS(a3),d3 get status register contents

 btst #b_DCD,d3 DCD already low?
 beq SetSta30 .. yes, send the signal

SetSta20 move.w (sp)+,sr
 OS9svc F$Send
 moveq.l #0,d1
 rts

SetSta30
 movem.w d0-d2,DCDPrc(a2) save proc id, code and path
 ori.w #CDEV,SCCM(a3) set for interrupt on change in DCD
 move.w (sp)+,a7
 rts (carry clear)

 else
SetSta_B equ *
 endc DCDSIG

 pag
* change path options
*
SetSta_D cmpi.w #SS_Opt,d0 set options call ?
 bne.s SetSta_E branch if not

* here to check whether baud/parity have changed

CheckMode: move.b PD_BAU(a1),d0 get baud rate current
 cmp.b BaudRate(a2),d0 has it changed ?
 bne.s ChngMode ..yes; attempt to re-configure
 move.b PD_PAR(a1),d1 get port configuration
 cmp.b Parity(a2),d1 has communication mode changed ?
 beq.s SetSta90 ..no; exit (nothing to do)
ChngMode:
 movea.l DPAddr(a2),a3 get 68360 base address
 move.b StopTX(a2),CR(a3) stop the transmitter
 move.l ConReg(a2),a0 get the offset to the config registers to d1
 andi.l #^(ENT+ENR),GSMR_L(a0) disable TX and RX
 move.b PD_BAU(a1),d0 get the new baud rate
 move.b d0,BaudRate(a2)
 bsr BaudCalc .. attempt to set it
 bcs.s ChngExit ..exit if error
 move.b PD_PAR(a1),d0 get parity, etc
 move.b d0,Parity(a2)
 bsr BitPar attempt to change communication mode
ChngExit move.w sr,-(sp) save ccr status
 move.b StartTX(a2),CR(a3)
 ori.l #ENT+ENR,GSMR_L(a0) re-enable TX and RX
 rtr restore ccr and return

* new path open
*
SetSta_E cmpi.w #SS_Open,d0 new path opened ?
 beq.s CheckMode ..yes; then check for configuration changes
 endc
* if we get here it's a call not supported by this driver
 bra Unknown

SetSta90 moveq.l #0,d1 clear Carry
 rts
 pag
********************
* Term:  Terminate 68360 SCC processing
*
* Passed:  (a1) = device descriptor pointer
*          (a2) = static storage
*          (a4) = current process descriptor ptr
*          (a6) = system global data ptr
*
* Returns:  none
*
* Error Return:  (cc) = carry set
*                d1.w = error code
*
TRMN00
 ifndef POLLED_IO
 move.w V_BUSY(a2),V_WAKE(a2) arrange wake up signal
 move.w (sp)+,sr restore IRQs
 pea.l Term(pc) return to entry point if signals
 bsr M68360Sleep wait for interrupt
 addq.l #4,sp toss return address if no signals

Term: move.w P$ID(a4),d0
 move.w d0,V_BUSY(a2)
 move.w d0,V_LPRC(a2)
 move.w sr,-(sp) save current IRQ status
 move.w IRQMask(a2),sr mask IRQs
 btst.b #H_Empty,OutHalt(a2) any data?
 beq.s TRMN00 .. yes, wait
*
* As we do not know if the SCC has completed the transfer of its internal
* buffers we need to sleep a while to allow for transmission to be completed.
*
* NOTE: The sleep time is specified in 'ticks' which are assumed to occur
* every 10 mS. If they occur differently the sleep value will need to be
* changed.
*
 moveq.l #3,d0
 cmpi.b #6,BaudRate(a2) Baud rate less than 600 baud?
 bhi.s Term20
 moveq.l #8,d0
Term20 OS9 F$Sleep
 movea.l DPAddr(a2),a3
 move.b StopTX(a2),CR(a3) stop the transmitter
 move.l ConReg(a2),a0 get config registers offset
 andi.w #^(TXEV+RXEV),SCCM(a0) stop TX and RX interrupts
 andi.l #^(ENT+ENR),GSMR_L(a0) disable TX and RX
 move.l CIMR(a3),d0
 move.l IVal(a2),d1 get 'interrupt enable' value for this SCC
 not.l d1
 and.l d0,d1
 move.l d1,CIMR(a3) disable this SCC's interrupts
Term40 move.w (sp)+,sr restore IRQ masks
 move.b M$Vector(a1),d0 get vector #
 suba.l a0,a0
 OS9 F$IRQ remove IRQ from polling tbl
 rts

 else POLLED_IO defined

Term:
 rts

 endc

 pag

********************
* M68360IRQ:  Process interrupt (input or output) from 68360 SCC
*
* Passed:  (a2) = Static Storage addr
*          (a3) = port address ptr
*          (a6) = system global data ptr
*
* Returns:  (cc) = carry set if false interrupt, else clear
*
* Destroys:  May only destroy D0, D1, A0, A2, A3 and A6.  Any
*            other registers used MUST be preserved.
*
M68360IRQ:
 movem.l	a1/a4,-(sp)
 movea.l DPAddr(a2),a4 point to DP RAM
 move.l ConReg(a2),a0 get config register offset to d1
 move.w SCCE(a0),d0 Get the SCC pending interrupts

 btst #b_RXEV,d0 receive interrupt?
 bne InIRQ yes

 btst #b_TXEV,d0 transmit interrupt?
 bne.s OutIRQ

 ifdef DCDSIG
 btst #b_CDEV,d0 DCD status changed?
 bne.s DCDIRQ
 endc DCDSIG

 tst.w d0
 beq.s NotOurs
 move.w	#$ffff,SCCE(a0)
 move.l	IVal(a2),d0
 move.l d0,CISR(a4)
 movem.l	(sp)+,a1/a4
 clr.l	d0
 rts

* Not an interrupt we were expecting if we get here
NotOurs: 
 movem.l	(sp)+,a1/a4
 ori.b #Carry,ccr return with carry set
 rts

 pag
********************
* OutIRQ:  68360 SCC output interrupt service
*
* Passed:  (a0) = scc config register
*		   (a2) = static storage address
*          (a3) = 68360 base address
*		   (a4) = DPRAM addr
*          (a6) = system globals
*          d1.w = offset to configuration registers
*
OutIRQ: 
 move.w #TXEV,SCCE(a0) reset the bit in the event register
 move.l	IVal(a2),CISR(a4)
 clr.l d1
OutI_a move.w OutCount(a2),d1 Get data count
 beq.s OutIRQ2 .. no chars to send
 movea.l OutEmpty(a2),a1 get pointer to next char
 move.w	TBASE(a3),d0
 add.w	d0,a4
 move.l	4(a4),a0
 move.b (a1)+,(a0)
 move.w	#1,2(a4)
 move.w #TXR+TXW+TXI,(a4) transmit data
 subq.w	#1,OutCount(a2) 
 cmpa.l OutEnd(a2),a1
 blo.s outirq10
 lea.l OutBuf(a2),a1 point to start
outirq10
 move.l	a1,OutEmpty(a2)	point to next char 
 bra.s	WakeUp
OutIRQ2 bset.b #H_Empty,OutHalt(a2) flag halted; buffer empty
 andi.w #^TXEV,SCCM(a0) disable transmit interrupts
WakeUp: move.w V_WAKE(a2),d0 Owner waiting?
 beq.s WakeUp10 ..no; return
 clr.w V_WAKE(a2)
 moveq.l #S$Wake,d1 Wake up signal

SendSig: OS9svc F$Send wake up process

WakeUp10
 movem.l	(sp)+,a1/a4
 moveq.l #0,d1
 rts

 pag
********************
* InIRQ:  68360 SCC input interrupt service
*
* Passed:  (a0) = scc config register
*		   (a2) = static storage address
*          (a3) = 68360 base address
*		   (a4) = DPRAM addr
*          (a6) = system globals
*          d1.w = offset to configuration registers
*
* Notice the Absence of Error Checking Here
*
InIRQ:
 move.w #RXEV,SCCE(a0) reset the bit in the event register
 move.l	IVal(a2),CISR(a4)
 clr.l	d1
 move.w	RBASE(a3),d1
 add.l d1,a4
 move.l	4(a4),a1
 move.b (a1),d0 get the RX data
 move.w #RXE+RXI+RXW,(a4) signal buffer available to CP
 tst.b d0 is it a null byte?
 beq.s InIRQ5 .. yes, so cannot be a control code
 cmp.b V_XON(a2),d0 X-ON continue?
 beq InXON ..Yes
 cmp.b V_XOFF(a2),d0 X-OFF Immediate Pause request?
 beq InXOFF ..Yes
 cmp.b V_INTR(a2),d0 keyboard Interrupt?
 beq InAbort ..Yes
 cmp.b V_QUIT(a2),d0 keyboard Quit?
 beq InQuit ..Yes
 cmp.b V_PCHR(a2),d0 keyboard Pause?
 beq InPause ..Yes
InIRQ5 
 movea.l InFill(a2),a1 point to current char
 move.b d0,(a1)+  write char to buffer
 addq.w #1,InCount(a2) count character
 cmpi.w #InpSiz,InCount(a2) buffer full?
 bls.s InIRQ10 branch if not
 subq.w #1,InCount(a2) uncount character
 addq.b #1,V_ERR(a2) show error
 bra WakeUp exit

InIRQ10 cmp.l InEnd(a2),a1 end of buffer?
 blo.s InIRQ30 branch if not
 lea.l InpBuf(a2),a1 point to start of buffer
InIRQ30 move.l a1,InFill(a2) update next in pointer
 move.w SigPrc(a2),d0 any process to notify?
 beq.s InIRQ40 ..no
 move.w SigPrc+2(a2),d1 get signal code
 clr.w SigPrc(a2) disable signal sending
 bra SendSig signal waiting process & return

InIRQ40 clr.w d0
 move.b V_XOFF(a2),d0 get X-OFF char
 beq WakeUp .. not enabled, exit
 cmpi.w #MaxBuff,InCount(a2) is buffer almost full?
 blo WakeUp .. no
 move.b InHalt(a2),d1 have we sent XOFF?
 bne WakeUp yes then don't send it again
 bclr.l #Signbit,d0 insure Sign clear
 move.b d0,V_XOFF(a2)
 ori.b #Sign,d0 set Sign bit
 move.b d0,InHalt(a2) flag input halt
 ori.w #CCE+CCREA+CCI,d0 set for XOFF char transmission
 move.w d0,CHARACTER8(a3)
 andi.w #^RXEV,SCCM(a0)
 bra WakeUp
 pag

********************
* Control character routines
*
* Passed:  (a2) = static storage address
*          (a3) = 68360 base address
*          (a6) = system globals
*          d1.w = offset to configuration registers
*

InPause: tst.l V_DEV2(a2) any echo device?
 beq InIRQ5 buffer char and exit if not
 move.l a1,-(sp) save it
 movea.l V_DEV2(a2),a1 get echo device static ptr
 move.b d0,V_PAUS(a1) request pause
 move.l (sp)+,a1 restore it
 bra InIRQ5 buffer char and exit

InAbort: moveq.l #S$Intrpt,d1 keyboard INTERRUPT signal
 bra.s InQuit10

InQuit: moveq.l #S$Abort,d1 Abort signal

InQuit10 move.b d0,-(sp) save input char
 move.w V_LPRC(a2),d0 last process ID
 beq.s InQuit90 ..none; exit
 clr.w V_WAKE(a2)
 OS9svc F$Send send signal to last user
InQuit90 move.b (sp)+,d0 restore input char
 bra InIRQ5 buffer char, exit

InXON: bclr.b #H_XOFF,OutHalt(a2) enable output
* re-enable the transmit ints
 ori.w #TXEV,SCCM(a0)
 sub.w RBASE(a3),a4 calculate pointer to xmit bd
 add.w TBASE(a3),a4 
 move.w (a4),d1 is a character currently being transmitted?
 btst #15,d1
 bne WakeUp .. yes, wait for it to interrupt after it's gone

here:
 sub.w TBASE(a3),a4	adjust base address for OutIRQ
 bra OutI_a

InXOFF: bset.b #H_XOFF,OutHalt(a2) flag output restricted
* disable transmit ints
 andi.w #^TXEV,SCCM(a0)
 moveq.l #0,d1
 bra WakeUp

 ifdef DCDSIG
DCDIRQ: move.w DCDPrc(a2),d0 any process to signal?
 beq WakeUp .. no
 andi.w #^CDEV,SCCM(a3) disable interrupt on change in DCD
* we don't need to check the current status as the interrupt is due to
* status changing. If the status had been as requested then the signal
* would have been sent immediately and the interrupt would not have
* been enabled.
 move.w DCDPrc+2(a2),d1 get the signal code
 clr.w DCDPrc(a2) disable further signal sending
 bra SendSig
 endc DCDSIG

 ends
